//============================================================
//= rAthenaCN 脚本汉化 转自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Bullet Dealers
//===== By ===================================================
//= Playtester, Paradox924X
//===== Version ==============================================
//= 2.0
//===== Compatible With ======================================
//= rAthena Project
//===== Description ==========================================
//= Bullet trader.
//===== Comments =============================================
//= 1.0 First version [Playtester]
//= 1.1 Converted from Aegis [Paradox924X]
//= 1.2 More optimized conversion [Legionaire]
//= 1.2a Removed .GATs [Lupus]
//= 1.3 Fixed [Playtester] Optimized. Got rid of @vars [Lupus]
//= 1.4 Updated to match AEGIS script. [Kisuka]
//= 1.5 Updated to match AEGIS script again. [Masao]
//= 1.6 Moved Izlude duplicate to pre-re/re paths. [Euphy]
//= 2.0 Clean-up. [Capuche]
//============================================================

alberta,118,160,3	script	弹药经销商 托尼#alb::bdt	4_M_04,{
	mes "[托尼]";
	if (BaseClass == Job_Gunslinger) {
		mes "我是弹药经销商托尼. ";
		mes "当您的子弹不够的时候就请来";
		mes "找我. 只要给我一些材料, ";
		mes "我就能为您制造您想要的子弹.";
		next;
		switch(select("- 毒子弹:- 火枪子弹:- 雷击子弹:- 失明子弹:- 冰冻子弹:- 取消")) {
		case 1: callsub S_BulletTrade,937,10,13205;// <item required>, <amount req>, <bullet id>
		case 2: callsub S_BulletTrade,7097,2,13203;
		case 3: callsub S_BulletTrade,7053,3,13204;
		case 4: callsub S_BulletTrade,1024,5,13206;
		case 5: callsub S_BulletTrade,7054,2,13207;
		case 6:
			mes "[托尼]";
			mes "改变主意了吗?";
			mes "好吧, 如果您还要子弹, ";
			mes "我一直在这里. ";
			mes "如果您需要更多的子弹";
			mes "可以随时回来这里";
			mes "找我, 枪手. ";
			close;
		}
	}
	mes "嘿, 你好我是托尼, 我负责";
	mes "帮枪手分配并制作子弹. ";
	mes "这只是";
	mes "我们工会喜欢的";
	mes "一种形式方式";
	mes "而已.";
	next;
	mes "[托尼]";
	mes "对不起, 如果您";
	mes "是来这里买子弹的. ";
	mes "我这只和已经";
	mes "转职的枪手交易. ";
	close;

S_BulletTrade:
	.@required_id = getarg(0);
	.@required_amount = getarg(1);
	.@bullet_id = getarg(2);

	mes "[托尼]";
	mes "对于一组";
	mes callfunc("F_InsertPlural",30,getitemname(.@bullet_id))+",";
	mes "你需要支付我";
	mes "一个 强化武器金属-级数二 , ";
	mes "一个 强化武器金属-级数一 , 以及";
	mes callfunc("F_InsertPlural",.@required_amount,getitemname(.@required_id))+".";
	next;
	mes "[托尼]";
	mes "请记得我一次";
	mes "最多只能给您500组";
	mes "30发的弹药. 请输入";
	mes "您想要兑换的";
	mes "弹药数量. 如果您想要";
	mes "取消, 只需要输入''0.''";
	next;
	input .@amount;
	mes "[托尼]";
	if (.@amount < 1 || .@amount > 500) {
		mes "嘿!我不能给您那么多子弹. ";
		mes "如果您真的想要";
		mes "用物品来兑换子弹";
		mes "就请记得";
		mes "不要输入超过";
		mes "500的数字.";
	}
	else if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(.@required_id) >= (.@amount*.@required_amount)) {
		if (checkweight(.@bullet_id,.@amount * 30) == 0) {
			mes "呃?你的背包里好像";
			mes "没有足够的空间来存放";
			mes "这些子弹. 等你清理出";
			mes "更多的空间的时候, 再来";
			mes "找我. 你可以把一些东西";
			mes "存放到卡普拉仓库中.";
		} else {
			mes "太好了, 似乎一切都";
			mes "井井有条的样子. 让我用";
			mes "这些材料帮您兑换";
			mes "子弹. 很高兴能和您";
			mes "做生意~";
			delitem 1010,.@amount; //Phracon
			delitem 1011,.@amount; //Emveretarcon
			delitem .@required_id,.@amount * .@required_amount;
			getitem .@bullet_id,.@amount * 30;
		}
	} else {
		mes "呼, 看起来您没有";
		mes "足以制作那么多子弹的";
		mes "材料. 好吧, ";
		mes "没关系. 等您集齐了";
		mes "所有需要的材料, ";
		mes "再来找我吧, 好吗?";
	}
	close;
}
