//============================================================
//= rAthenaCN [卡普拉人员]汉化 
//============================================================

//===== rAthena Script =======================================
//= Cool Event Corp. Staff
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Cool Event Corp. Staff (Save, Storage & Pushcart Service)
//= in Arunafeltz Region.
//===== Additional Comments: ================================= 
//= 1.0 First version, iRO Sakray X.4 [Gepard]
//= 1.1 Optimized, fixed a conditional bug [Lupus]
//= 1.1a Redo back. It wasn't a bug [Lupus]
//= 1.2 Added Rachel Cool Event Corp. Staff [Playtester]
//= 1.3 Added cutins, closed Kafra Password exploit
//=     until we got a client/packet based password support [Lupus]
//= 1.4 Added Veins Cool Event Corp. Staff. [L0ne_W0lf]
//= 1.5 Cool Event Corp staff is now more iRO like. [L0ne_W0lf]
//= 1.6 Wrote a basic function and added VIP features. [Euphy]
//============================================================ 

//callfunc "F_CoolEventCorp","<menu>","<location>","<save map>",<x>,<y>,"<warp map>";
function	script	F_CoolEventCorp	{
	cutin "zonda_01",2;
	mes "[准它企划社社员]";
	mes "欢迎来到准它企划社！";
	mes "我们的社员提供您24小时的优质服务";
	mes "来满足您的各项需求";
	mes "所以";
	mes "有什么我能为您做的吗？";
	next;
	switch(select(getarg(0))) {
	case 1:
		mes "[准它企划社社员]";
		mes "您的储存点";
		mes "现已保存在";
		mes "毁葛";
		mes "谢谢您使用";
		mes "准它企划社的服务.";
		savepoint getarg(2),getarg(3),getarg(4);
		close2;
		break;
	case 2:
		mes "[准它企划社社员]";
		if(!callfunc("F_CanOpenStorage")){
			mes "对不起";
			mes "您的基础技能等级";
			mes "不足6级";
			mes "无法使用仓库服务.";
			close2;
			break;
		}
		if(Zeny < 40){
			mes "对不起";
			mes "您身上的金币不足";
			mes "无法使用仓库服务";
			mes "仓库服务需要40Zeny";
			close2;
			break;
		}
		set Zeny, Zeny - 40;
		mes "让我帮您打开";
		mes "您的个人仓库";
		mes "谢谢使用准它企划社";
		mes "为您提供的服务";
		mes "祝您度过愉快的一天";
		callfunc("F_CheckKafCode");	//check your storage password, if set
		close2;
		openstorage;
		break;
	case 3:
		mes "请选择";
		mes "您的目的地.";
		next;
		if (VIP_SCRIPT && !vip_status(1))
			set .@cost,4400;
		else
			set .@cost,2200;
		.@destination$ = getarg(5);
		if (select(.@destination$ +" -> "+ .@cost +" z:Cancel") == 1) {
			if ((countitem(7060) != 0) || (Zeny >= .@cost)) {
				if (countitem(7060) != 0) {
					delitem 7060,1; //Warp_Free_Ticket
				}
				else {
					set Zeny,Zeny-.@cost;
				}
				if (.@destination$ == "Veins") warp "veins",205,101;
				else if (.@destination$ == "Rachel") warp "rachel",115,125;
				cutin "",255;
				end;
			}
			else {
				mes "[准它企划社社员]";
				mes "非常抱歉.";
				mes "您所携带的Zeny不够.";
				mes "无法使用传送服务.";
				mes "to "+ .@destination$ +" is "+.@cost+" zeny.";
			}
		}
		close2;
		break;
	case 4:
		mes "[准它企划社社员]";
		if(BaseClass != Job_Merchant){
			mes "对不起";
			mes "手推车租用服务";
			if (checkre(0))
				mes "只提供给商人.";
			else {
				mes "只提供给商人,";
				mes "铁匠, 银匠,";
				mes "炼金术师以及创造者.";
			}
			close2;
			break;
		}
		if(getskilllv("MC_PUSHCART") == 0){
			mes "您需要先学习手推车技能";
			mes "才可以租用手推车.";
			close2;
			break;
		}
		if(checkcart() == 1){
			mes "您已经拥有";
			mes "一辆手推车了.";
			mes "不幸的是, 我们无法";
			mes "租借更多的手推车";
			mes "给已经拥有的用户.";
			close2;
			break;
		}
		if(countitem(7061)){
			delitem 7061,1; //Cart_Free_Ticket
			setcart;
			close2;
			break;
		}
		mes "租用手推车的";
		mes "价格为800Zeny";
		mes "您想租用手推车吗？";
		next;
		if(select("租用一辆手推车:取消") == 1){
			if(Zeny < 800){
				mes "[准它企划社社员]";
				mes "对不起.";
				mes "您没有足够的Zeny";
				mes "来支付";
				mes "租用手推车的费用.";
			} else {
				set Zeny, Zeny - 800;
				setcart;
			}
		}
		close2;
		break;
	case 5:
		callfunc("F_SetKafCode","[准它企划社社员]","准它企划社.");
		//the function closes msg and removes cutins
	case 6:
	default:
		mes "[准它企划社社员]";
		mes "准它企划社一直以来";
		mes "都在竭诚提供给顾客";
		mes "最为优质的服务.";
		mes "帮助我们最好的办法";
		mes "就是提供给我们您的";
		mes "意见以及忠实的反馈.谢谢.";
		close2;
	}
	cutin "", 255;
	end;
}

hugel,88,168,4	script	准它企划社社员::CoolEventCorpStaffHugel	874,{
	callfunc "F_CoolEventCorp",
		"储存位置:使用仓库::租用手推车:仓库密码服务:取消",
		"in the village of Hugel","hugel",96,155;
	end;
}

rachel,109,138,5	script	准它企划社社员::CoolEventCorpStaffRachel	874,{
	callfunc "F_CoolEventCorp",
		"储存位置:使用仓库:传送服务:租用手推车:仓库密码服务:取消",
		"in the town of Rachel","rachel",113,137,"Veins";
	end;
}

veins,208,128,5	script	准它企划社社员::CoolEventCorpStaffVeins	874,{
	callfunc "F_CoolEventCorp",
		"储存位置:使用仓库:传送服务:租用手推车:仓库密码服务:取消",
		"in the town of Veins","veins",204,103,"Rachel";
	end;
}
