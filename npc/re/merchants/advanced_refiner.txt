//============================================================
//= rAthenaCN 脚本汉化 转自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Advanced Refiner
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses Enriched ores to increase upgrade success.
//= After a conversation with Doddler, it's been established that
//= the advanced refiner works similar the the "Bubble Gum" item.
//= The success percentage is not "increased" however, if it fails
//= You get a second try. This tries twice at the same time,
//= effectively giving you a re-roll on your attempt.
//===== Additional Comments: =================================
//= 1.0 Added Malangdo Refiner "Holink". [Euphy]
//============================================================

// Main NPC :: mal_jerun
//============================================================
malangdo,221,174,6	script	霍林克#mal_cash	559,{
	disable_items;
	mes "[霍林克]";
	mes "我是 '喵~' 铁匠霍林克~";
	mes "精炼界的大师, 霍林克~";
	mes "我是一只特殊的猫, 师承梦罗克的精炼大师~";
	mes "我的女儿是如此为我骄傲, 霍林克~";
	mes "无所不能的霍林克~ 今天想精炼吗?";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for (set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$ + ( getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +" - [未装备]" ) +":";
	set .@part, .@indices[ select(.@menu$) ];

	if (!getequipisequiped(.@part)) {
		mes "[霍林克]";
		switch(.@part) {
		case 1:
			mes "我的师傅龙仙儒曾经说过, 喵~";
			mes "天生我才必有用...";
			break;
		case 2:
			mes "这里没什么好看的, 喵!!";
			break;
		case 3:
			mes "喵? 你想让我怎么处理这件装备在左手的物品...?";
			break;
		case 4:
			mes "喵? 你想让我怎么处理这件装备在右手的物品...?";
			break;
		case 5:
			mes "喵? 你竟然什么都没穿.";
			break;
		case 6:
			mes "Kyang~! 别想瞒过我敏锐的嗅觉, 喵~.";
			break;
		case 7:
		case 8:
			mes "喵? 装饰品在哪里?";
			break;
		case 9:
		case 10:
			mes "喵? 你想知道头部其他位置的精炼方法, 喵?~";
			break;
		}
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[霍林克]";
		mes "即使我师傅龙仙儒也无法精炼这个东西, 喵.";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[霍林克]";
		mes "喵~ 完美的精炼. 龙仙儒都做不到, 喵?~";
		close;
	}
	mes "[霍林克]";
	switch(getequipweaponlv(.@part)) {
	default:
	case 0: // Armor
		set .@price,15000;
		set .@material,7619; //Enriched_Elunium
		set .@type$,"armor";
		mes "你想要精炼身上的铠甲, 喵~";
		break;
	case 1: // Level 1 Weapon
		set .@price,500;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "呃, 一个1级武器...?";
		break;
	case 2: // Level 2 Weapon
		set .@price,2000;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "喵, 一个2级武器...?";
		break;
	case 3: // Level 3 Weapon
		set .@price,20000;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "喵 喵~~ 一个3级武器~~";
		break;
	case 4: // Level 4 Weapon
		set .@price,50000;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "咪-喵!... 一个4级武器...!";
		mes "我只见过2次这玩意.";
		mes "还是从龙师傅那里学来的... 咪-喵!!";
		break;
	}
	mes "这次精炼你得付 ^ff9999"+getitemname(.@material)+"^000000 和 ^ff9999"+.@price+"^000000 Zeny, 喵~";
	mes "Want to continue, 喵?~";
	next;
	if(select("Yes!!:No!!") == 2) {
		mes "[霍林克]";
		mes "Kyack!!";
		mes "你竟然不相信精炼大师霍林克, 喵?~";
		close;
	}
	if (getequippercentrefinery(.@part) < 100) {
		mes "[霍林克]";
		mes "喵!!";
		if (.@type$ == "armor")
			mes "这件铠甲已经不知道被精炼过多少次了, 喵.";
		else {
			mes "危险. 危险~";
			mes "这把武器小小的精炼过几次, 喵~";
			next;
			mes "[霍林克]";
		}
		mes "如果你想继续追求这件装备的高精炼值的话.";
		mes "那很有可能会在精炼过程中饮恨, 喵.";
		next;
		mes "[霍林克]";
		mes "如果 "+.@type$+" 给精炼坏了, ";
		mes "你可以就再也用不了了, 喵. 还有... ";
		mes "^ff0000卡片和附魔^000000也会一起玩完.";
		mes "后果无法预料, 你还想继续尝试下去吗, 喵~?";
		next;
		if(select("- 是的我要继续精炼!!:- 好吧我再想想!!") == 2) {
			mes "[霍林克]";
			mes "喵! 明智的选择, 喵.";
			mes "不过!!";
			mes "我对于你如此轻视我精炼大师霍林克而提出抗议, 喵~";
			close;
		}
	}
	if (countitem(.@material) == 0 || Zeny < .@price) {
		mes "[霍林克]";
		mes "你的材料没带够啊.";
		mes "你需要为此支付 ^ff9999"+getitemname(.@material)+"^000000 和 ^ff9999"+.@price+"^000000 Zeny, 喵~";
		mes "搞定这些再来吧, 喵~";
		close;
	}
	delitem .@material,1;
	set Zeny, Zeny-.@price;
	if (getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100)) {
		successrefitem .@part;
		mes "[霍林克]";
		mes "咪~ 咪~ 喵! 快乐 兴奋 精炼中~";
		next;
		emotion ET_CHUP;
		mes "[霍林克]";
		mes "完美!! 无敌, 喵!!";
		mes "我是龙仙儒的高徒~";
		mes "霍林克!!";
		mes "又是一个成功的精炼日, 喵!!";
		close;
	}
	failedrefitem .@part;
	mes "[霍林克]";
	mes "喵~ 喵~ Kyaaak!!";
	next;
	switch(rand(1,5)) {
		case 1: emotion ET_CRY; break;
		case 2: emotion ET_PROFUSELY_SWEAT; break;
		case 3: emotion ET_KEK; break;
		case 4: emotion ET_SCRATCH; break;
		case 5: emotion ET_BIGTHROB; break;
	}
	mes "[霍林克]";
	mes "喵!! Aaaaakk~~!!!!";
	mes "Kyaaak!! 我失败了, 喵!!";
	next;
	mes "[霍林克]";
	mes "......";
	mes "......";
	mes "所有~ 一切~ 都玩完了, 喵...";
	next;
	mes "[霍林克]";
	mes "喵.... 我师傅龙仙儒曾经说过.";
	mes "失败乃成功之母...";
	mes "人类, 要记住, 这次失败会是你今后成功的开始.";
	close;
}
