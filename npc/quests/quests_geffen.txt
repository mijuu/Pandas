//===== Hercules Script ======================================
//= Quest NPCs related to Geffen
//===== By: ==================================================
//= kobra_k88 & Samuray22
//===== Current Version: =====================================
//= 1.5
//===== Description: =========================================
//= Welding Mask and Headset Quests.
//===== Additional Comments: =================================
//= 1.1 Fixed exploits [Lupus]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Updated "Welding Mask" Quest to the Official one. [Samuray22]
//= 1.3a replaced item "names" with item id. Added missing [Lupus]
//= 1.4 Updated welding mask and headset quests. [L0ne_W0lf]
//= 1.5 Added Number Match Game. [Euphy]
//============================================================

// Blacksmith: 'Welding Mask' Quest
//============================================================
geffen_in,144,166,4	script	铁匠	1_M_SMITH,{
	mes "[铁匠]";
	mes "你没有听说过我?";
	mes "为什么, 我是这里的资深铁匠.";
	mes "我在这热与血腥的地狱之火奋斗了30年, ";
	mes "我有无法弯曲的钢铁意志!";
	next;
	mes "[铁匠]";
	mes "但是...";
	mes "我的力量随着我年龄的老去而消失着.";
	mes "所以我请来了一些年轻的, 热血的, ";
	mes "上进的铁匠, 希望他们掌握我的技术...";
	next;
	switch(select("这是什么?:...")) {
	case 1:
		mes "[铁匠]";
		mes "我们铁匠经常在极度高温的环境下打铁, 这些炎热的气温和融化的钢铁不是一般人可以忍耐的.";
		next;
		mes "[铁匠]";
		mes "为了我们的艺术, 我们的美丽, 我们的脸部时常处于危险中...";
		next;
		mes "[铁匠]";
		mes "但随着一个非凡的技术诞生, 这些问题再也没有需要担忧!";
		mes "看, 这是就是我发明的^3355FF钢铁面罩^000000";
		next;
		mes "[铁匠]";
		mes "它由可以覆盖脸部的厚金属板构成.";
		mes "事实上, 这是所有人都想要的道具!";
		mes "如果我不是自己要用, 给一些正在学习打铁的朋友也不失为一个好主意.";
		next;
		mes "[铁匠]";
		mes "这样如何?如果你给我^2244FF50个钢铁^000000和^4422FF2000金币^000000, 我会马上为你做一个~";
		next;
		switch(select("嗯...不错的样子, 好吧:这是怎么工作的能再说一遍么?")) {
		case 1:
			if ((countitem(999) > 49) && (Zeny > 1999)){
				mes "[铁匠]";
				mes "啊, 我发现你很欣赏铁匠的工作! 很好, 我马上做给你";
				next;
				delitem 999,50;  //Steel
				set Zeny, Zeny - 2000;
				getitem 2292,1; // Welding_Mask
				mes "[铁匠]";
				mes "哈哈哈~!好好用它哦!";
				close;
			}
			else {
				mes "[铁匠]";
				mes "嗯...很抱歉~";
				mes "你没有我需要的道具.";
				next;
				mes "[铁匠]";
				mes "正常的铁匠会把这当成是不尊重的信号, 但我并没有觉得你对铁匠有不敬的感觉.如果你还想我做面具的话就别忘了我需要的东西.";
				close;
			}
		case 2:
			mes "[铁匠]";
			mes "^3355FF钢铁面具^000000用覆盖你脸部的厚铁板来保护你的脸.本发明是我在铁锤和铁砧中无休止地工作30年的结果.";
			close;
		}
	case 2:
		mes "[铁匠]";
		mes "啊...!";
		mes "我相信你第一次遇到我这么好的铁匠, ";
		mes "所以敬畏得说不出话.";
		mes "当你准备好的时候请找个时间告诉我.";
		next;
		mes "^3355FF这家伙似乎有点自满~^000000";
		close;
	}
}

// Eric: 'Headset' Quest
//============================================================
geffen_in,30,71,4	script	郑何意嘉	4_M_01,{
	mes "[郑何意嘉]";
	mes "你要不要听听我爷爷说的故事呢?";
	next;
	switch (select("继续对话:询问材料:制作看看:取消")) {
	case 1:
		mes "[郑何意嘉]";
		mes "前几年刚过世的爷爷";
		mes "在我的生命中是个相当重要的人物";
		mes "比起我那忙碌的父亲";
		mes "我可以感觉到爷爷依旧在我身边守候着我";
		next;
		mes "[郑何意嘉]";
		mes "爷爷的书房更是最令我感到有趣的地方";
		mes "就像是' 世外桃源 '一般";
		mes "每当我想念我爷爷的时候";
		mes "我就会到书房去找些书来看";
		next;
		mes "[郑何意嘉]";
		mes "在那里";
		mes "我可以透过书籍学习到许多的知识";
		mes "因为爷爷对于炼金术和音乐都相当的喜爱";
		mes "所以书房里头几乎满满的都是相关的书籍";
		next;
		mes "[郑何意嘉]";
		mes "直到有一天 ..";
		mes "我像往常一样进到爷爷的书房里";
		mes "在那里我偶然发现了一张相当神奇的设计图";
		next;
		mes "[郑何意嘉]";
		mes "在设计图中有一样东西, 如果将它像耳罩一样戴上";
		mes "如果将它像耳罩一样戴, 透过一条细线的牵引";
		mes "将可以听到像留声机中一样的声音";
		mes "这就是设计图的内容";
		next;
		mes "[郑何意嘉]";
		mes "我 ..一定要完成爷爷的遗愿";
		mes "做出这样东西来";
		mes "所以我到处去将这设计图给其他炼金者看";
		mes "请求他们帮忙, 但是所有的人都说不知道这是什么东西";
		next;
		mes "[郑何意嘉]";
		mes "所以我才会想要亲自动手来做做看";
		mes "经过我多年的苦心研究..";
		mes "我已经可以完全理解那图上的结构了";
		mes "不过 .. 我还没能收集到所需要的材料";
		next;
		mes "[郑何意嘉]";
		mes "如果现在手边有材料的话";
		mes "我就可以马上依照设计图动手制作了";
		mes "如果 .. 你感兴趣的话";
		mes "是不是可以帮我找到所需要的材料呢?";
		next;
		mes "[郑何意嘉]";
		mes "我只是想要完成爷爷的遗愿";
		mes "做出设计图上的东西而已";
		mes "完成后如果你要的话, 给你也没关系";
		close;
	case 2:
		mes "[郑何意嘉]";
		mes "想完成爷爷的遗愿, 我需要这些东西：";
		mes "^3355FF钢铁^000000 ^FF555540^000000 片";
		mes "^3355FF神之金属^000000 ^FF55551^000000 个";
		mes "^3355FF酒精^000000 ^FF55551^000000 瓶";
		mes "^3355FF煤矿^000000 ^FF55551^000000 块";
		close;
	case 3:
		if ((countitem(999) > 39) && (countitem(984) > 0) && (countitem(970) > 0) && (countitem(1003) > 0)) {
			delitem 999,40; //Steel
			delitem 984,1; //Oridecon
			delitem 970,1; //Alchol
			delitem 1003,1; //Coal
			mes "滴答..滴答..滴答..";
			next;
			getitem 5001,1; //Headset
			mes "[郑何意嘉]";
			mes "真是太感谢你了";
			mes "托你的福我才能完成爷爷的遗愿";
			mes "如果爷爷能看到的话一定会很开心的";
			next;
			mes "[郑何意嘉]";
			mes "诚如我之前说过的, 完成品请你带走吧!";
			mes "既然已经完成了, 我就感到很高兴了";
			close;
		}
		else {
			mes "[郑何意嘉]";
			mes "谢谢你的好意 .. 但是材料似乎不足";
			mes "请你备齐了再来吧!";
			close;
		}
	case 4:
		mes "[郑何意嘉]";
		mes "不想听吗?";
		mes "好吧...或许你有你自己的想法~";
		mes "请慢走";
		close;
	}
}

// Number Match Game :: gef_yagu
//============================================================
geffen,122,77,4	script	Nia#yagu	8_F_GIRL,{
	mes "[Nia]";
	mes "Hello, I'm Nia, the fifth";
	mes "disciple of Mawong, the";
	mes "gaming mastermind. Now,";
	mes "how would you like to play a";
	mes "Number Match game with me?";
	next;
	switch(select("Number Match Game?:Practice Number Match:Play Number Match:Record Holder:Cancel")) {
	case 1:
		mes "[Nia]";
		mes "Ah, well, the objective of";
		mes "the Number Match game is to";
		mes "guess the number I'm thinking";
		mes "of. Of course, there are a few";
		mes "rules that limit the numbers";
		mes "that I can choose from.";
		next;
		mes "[Nia]";
		mes "All the numbers that I make";
		mes "up must be 3 digits, I can't";
		mes "use 0 for any of the digits,";
		mes "and I can't use the same";
		mes "number for any of the digits";
		mes "more than once.";
		next;
		mes "[Nia]";
		mes "So, for example, I can't";
		mes "use the numbers 103, 112,";
		mes "252, or 701. Those numbers";
		mes "either have a 0, or they use";
		mes "the same number in the digits";
		mes "more than once. Got it now?";
		next;
		mes "[Nia]";
		mes "Now, you only get 5 guesses,";
		mes "and after each guess, I'll tell";
		mes "you how many digits you guessed";
		mes "correctly, followed by the number of correctly guessed digits in";
		mes "the correct digit place.";
		next;
		mes "[Nia]";
		mes "For example, let's say the";
		mes "number I come up with is 168.";
		mes "If your first guess is 678, then you guessed two of the digits,";
		mes "6 and 8, correctly. However,";
		mes "only 8 is in the correct place.";
		next;
		mes "[Nia]";
		mes "You know, it'd be a good idea";
		mes "if you try the Practice Mode";
		mes "first. Then, when you're more";
		mes "comfortable, play the actual";
		mes "Number Match game. Don't forget that speed counts for your score!";
		next;
		mes "[Nia]";
		mes "Ah, if you happen to";
		mes "make a new record in the";
		mes "Number Match game, you'll";
		mes "have your name recorded,";
		mes "so don't miss this chance for";
		mes "fame and maybe even fortune~";
		close;
	case 2:
		mes "[Nia]";
		mes "Great, let's do a practice";
		mes "Number Match game. Please";
		mes "try to guess my number, okay?";
		mes "If you want to cancel, then please enter a number greater than 1,000.";
		set .@practice,1;
		break;
	case 3:
		mes "[Nia]";
		mes "Great, let's play Number Match!";
		mes "You will have 5 chances to guess the number that I'm thinking of.";
		mes "If you want to cancel, then please enter a number greater than 1,000.";
		set .@practice,0;
		break;
	case 4:
		set .@name_record$, $050908_minus1_yagu$;
		if (.@name_record$ == "") //custom
			set .@name_record$,"(null)";
		set .@score_record, $050908_minus1_yagu;
		set .@score_min, .@score_record / 60;
		set .@score_sec, .@score_record % 60;
		mes "[Nia]";
		mes "^ff0000" + .@name_record$ + "^000000 is the";
		mes "record holder for the Number";
		mes "Match game with a time of";
		mes "^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
		close;
	case 5:
		mes "[Nia]";
		mes "Take care of yourself,";
		mes "adventurer. If you ever";
		mes "feel like playing a Number";
		mes "Match game, come and talk";
		mes "to me, alright? See you~";
		close;
	}
	next;

	// Generate 3 digits of a number.
	while(1) {
		set .@yagu100, rand(1,9);
		set .@yagu10, rand(1,9);
		set .@yagu1, rand(1,9);
		if ((.@yagu100 != .@yagu10) && (.@yagu100 != .@yagu1) && (.@yagu10 != .@yagu1)) {
			// Store the actual number (unofficial).
			set .@yagu, (100*.@yagu100) + (10*.@yagu10) + .@yagu1;
			break;
		}
	}

	// The official time calculation is based on ServerTime,
	// an int HHMMSS equivalent to atoi(gettimestr("%H%M%S",7)).
	// UNIX time is really a better way to do this.
	set .@start_time, gettimetick(2);

	while(1) {

		// Guess a number that meets all conditions.
		while(1) {
			if (input(.@input,100,999) != 0) {
				mes "[Nia]";
				mes "You've canceled the";
				mes "Number Match game.";
				close;
			}
			set .@input100, .@input / 100;
			set .@input10, (.@input % 100) / 10;
			set .@input1, .@input % 10;
			if ((.@input100 > 0) && (.@input10 > 0) && (.@input1 > 0)) {
				if ((.@input100 != .@input10) && (.@input100 != .@input1) && (.@input10 != .@input1))
					break;
			}
			mes "[Nia]";
			mes "You entered one of the";
			mes "digits as 0, or input the";
			mes "same numerical value in";
			mes "the digits more than once.";
			mes "Please enter another guess.";
			next;
		}

		set .@retry, .@retry+1;
		mes "[Nia]";
		mes "^ff0000"+strcharinfo(0)+"^000000,";
		mes "your guess is ^0000ff"+.@input+"^000000.";
		mes "Give me a moment to";
		mes "come up with your results.";
		next;

		// Correct digits in correct locations.
		set .@strike,0;
		if (.@yagu100 == .@input100)
			set .@strike, .@strike+1;
		if (.@yagu10 == .@input10)
			set .@strike, .@strike+1;
		if (.@yagu1 == .@input1)
			set .@strike, .@strike+1;

		// Correct digits in incorrect locations.
		set .@ball,0;
		if ((.@yagu100 == .@input10) || (.@yagu100 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu10 == .@input100) || (.@yagu10 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu1 == .@input100) || (.@yagu1 == .@input10))
			set .@ball, .@ball+1;

		if (.@strike == 3) {
			if (.@practice) {
				mes "[Nia]";
				mes "Yes, the number that";
				mes "I guessed was ^ff0000"+.@yagu+"^000000!";
				mes "Congratulations, you";
				mes "just won this practice";
				mes "game! Now you should be";
				mes "ready for the real thing~";
				close;
			} else {

				// Calculate final time and check against the previous record.
				set .@end_time, gettimetick(2);
				if (.@end_time < .@start_time)
					set .@end_time, .@end_time + 3600;
				set .@total_time, .@end_time - .@start_time;
				set .@total_min, .@total_time / 60;
				set .@total_sec, .@total_time % 60;
				set .@name_record$, $050908_minus1_yagu$;
				if (.@name_record$ == "") //custom
					set .@name_record$,"(null)";
				set .@score_record, $050908_minus1_yagu;
				set .@score_min, .@score_record / 60;
				set .@score_sec, .@score_record % 60;

				if (.@score_record >= .@total_time || !.@score_record) { //custom (second condition)
					set $050908_minus1_yagu$, strcharinfo(0);
					set $050908_minus1_yagu, .@total_time;
					mes "[Nia]";
					mes "Great! You guessed";
					mes "my number, which was";
					mes "^ff0000" + .@yagu + "^000000. You finished in";
					mes "^ff0000" + .@total_min + " minutes, " + .@total_sec + " seconds^000000,";
					mes "breaking the previous record";
					mes "of ^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
					next;
					mes "[Nia]";
					mes "^ff0000" + .@name_record$ + "^000000";
					mes "set that old record,";
					mes "but now you are the";
					mes "new person to beat in the";
					mes "Match Game. Congratulations";
					mes "for setting a new record!";
					close;
				}
				mes "[Nia]";
				mes "Nice work, you guessed";
				mes "my number! The answer, of";
				mes "course, was ^ff0000" + .@yagu + "^000000. You managed";
				mes "to guess this number correctly";
				mes "in ^ff0000" + .@total_min + " minutes, " + .@total_sec + " seconds^000000.";
				next;
				mes "[Nia]";
				mes "^ff0000" + .@name_record$ + "^000000 is";
				mes "the current Number Match";
				mes "record holder with a time";
				mes "of ^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
				mes "Good luck, and hopefully you";
				mes "can break this record someday~";
				close;
			}
		} else {
			if (.@practice) {
				mes "[Nia]";
				mes "Your guess has ^ff0000" + .@ball + "^000000 of the";
				mes "correct numbers. ^ff0000" + .@strike + "^000000 of the";
				mes "digits in your guess have";
				mes "the correct number in the";
				mes "correct digit placement.";
				mes "Alright, try it again~";
				next;
			} else {
				mes "[Nia]";
				mes "Your last guess had ^ff0000" + .@ball + "^000000";
				mes "of the correct numbers,";
				mes "and ^ff0000" + .@strike + "^000000 digits in your guess";
				mes "had the correct number in";
				mes "the correct digit placement.";
				switch(.@retry) {
					case 1: mes "You have four guesses left."; break;
					case 2: mes "You have three guesses left."; break;
					case 3: mes "You have two guesses left."; break;
					case 4: mes "You only have one guess left..."; break;
					case 5: mes "The answer was ^ff0000" + .@yagu + "^000000."; close;
				}
				next;
			}
		}
	}
}
